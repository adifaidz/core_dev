| --------------------------------------------------------------------------
| autoShroom.mac
| harvests the kaladim fungi
| original macro was by psychotik
| Updates by BrainDeath, Exmarine
| cleanup by gimp
| --------------------------------------------------------------------------

#event Full "#*#There was no place to put that#*#"

Sub Main

	/echo Welcome to autoShroom
	/declare ShroomCount int outer
	/cleanup

	/notify actionsmainpage AMP_WalkButton leftmouseup
	/delay 1s
	/sit off
	/delay 1s

	:HarvestLoop
	/call CheckForPCs

	/for ShroomCount 1 to 9
  	/squelch /itemtarget *Blue
  	/if (${Ground.Distance}<150) {
    	/call MoveToTarget
    	/call GetIt
    	/doevents
  	}
  	/squelch /itemtarget *Red
  	/if (${Ground.Distance}<150) {
    	/call MoveToTarget
    	/call GetIt
    	/doevents
  	}
  	/call CheckForPCs 
	/next ShroomCount

	/notify actionsmainpage AMP_RunButton leftmouseup
	/delay 1s
	/squelch /target clear
	/face loc 661,-107
	/keypress CENTERVIEW
	/delay 2s

	/echo Next Harvest 6 minutes...
  /delay 1m
  /echo 5 minutes...
  /delay 1m
  /echo 4 minutes...
  /delay 1m
  /echo 3 minutes...
  /delay 1m
  /echo 2 minutes...
  /delay 1m
  /echo 1 minute...
  /delay 1m
	/echo Preparing to Harvest...
	/delay ${Math.Rand[300]}

	/doevents
	/delay 2s
	/notify actionsmainpage AMP_WalkButton leftmouseup
	/goto :HarvestLoop
/return

Sub MoveToTarget
	:KeepMoving
	/face loc ${Ground.Y}, ${Ground.X}
	/delay 1
	/keypress forward hold
	/if (${Ground.Distance}>15) /goto :KeepMoving
	:StopMoving
	/keypress forward
/return

Sub CheckForPCs
	/if (${SpawnCount[gm]}) {
  	/echo GM in Zone! ending
  	/endmacro
	}
	/if (${SpawnCount[pc radius 200 notid ${Me.ID}]}) {
  	/echo PC nearby, pausing
  	/delay 1m
	}
/return


Sub GetIt
	/click left item
	/delay 5s (${Cursor.ID})
	/delay ${Math.Rand[10]}
	/autoinv
	/delay 5s (!${Cursor.ID})
	/delay ${Math.Rand[10]}
	/doevents
/return

Sub Event_Chat
	/echo I got a tell, ending
	/endmacro
/return


Sub Event_Full
	/echo Your inventory is full, ending
	/endmacro
/return