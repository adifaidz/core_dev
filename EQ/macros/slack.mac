| druid normal slack
#define HEAL_ONLY_MODE			0
#define QHEAL_PERCENT				65
#define CHEAL_PERCENT				85
#define USE_CH							0


#include bot_handleassist.inc

Sub Main

	/declare SpawnId int local 0
	/declare assistRange int local 0
	/declare AbortLoop int outer 0
	/declare i int local 0

	/if (!${Raid.Members} && !${Me.LADelegateMA} && (!${Target.ID} || ${Target.Type.Equal[NPC]})) {
		/echo Delegate MA not available, target MT and rerun
		/endmacro
	}

	/declare MT int local 0
	/declare MTName string local
	
	/if (${Target.ID} && ${Target.Type.Equal["PC"]}) {
		/varset MT ${Target.ID}
		/varset MTName ${Target.Name}
	}

	/if (${Raid.Members} >= 18) {
		/varset assistRange 90
		/echo Raid slack.
	} else /if (${Me.LADelegateMA} && (${Group.Members}>1)) {
		/varset assistRange 70
		/echo Group slack, using Delegate MA method.
	} else {
		/varset assistRange 100

		/echo Delegate MA not available, assisting ${Spawn[id ${MT}].Name}
	}

	| /squelch /target clear
	
	/call Init_HandleAssist
	
	/echo Waiting for mobs
	
	:Loop
	/if (${Raid.Members} && (${Me.RaidAssistTarget[1].ID} || ${Me.RaidAssistTarget[2].ID} || ${Me.RaidAssistTarget[3].ID})) {
		/varset SpawnId 0
		/for i 1 to 3
			/if (${Me.RaidAssistTarget[${i}].ID} && ${Me.RaidAssistTarget[${i}].Type.Equal[NPC]} && ${Me.RaidAssistTarget[${i}].Distance} < ${assistRange}) /varset SpawnId ${Me.RaidAssistTarget[${i}].ID}
		/next i

		/if (${SpawnId} && !${Me.Moving}) {
			/call HandleAssistCall ${SpawnId}
		}
		
	} else /if (${Me.LADelegateMA} && ${Me.GroupAssistTarget.ID}) {

		/if (${Me.GroupAssistTarget.ID} && ${Me.GroupAssistTarget.Type.Equal[NPC]} && (${Me.GroupAssistTarget.Distance} < ${assistRange}) && !${Me.Moving}) {
			/call HandleAssistCall ${Me.GroupAssistTarget.ID}
		}

	} else /if (${MT}) {
		| Only assist MT if there is a npc nearby and tank is nearby aswell
		/if (${Spawn[npc radius ${assistRange}].LineOfSight} && !${Me.Moving} && (${Spawn[id ${MT}].Distance} < ${assistRange})) {
			/echo # Assisting ${MTName}
			/assist ${MTName}
			/delay 5
			|/delay ${Math.Calc[${Math.Rand[2]}+1].Int}s
			/if (${Target.ID} && ${Target.Type.Equal[NPC]}) {
				/call HandleAssistCall ${Target.ID}
			} else {
				| /if (${Target.ID}) /squelch /target clear
				/delay 1s
			}
		}
	}

	/doevents

	/call HandleGroupHeal QHEAL_PERCENT

	/call UpdateBuffs

	/goto :Loop

/return