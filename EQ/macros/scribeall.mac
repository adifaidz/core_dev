| memorizes all spells in inventory that is not already in spellbook

Sub Main
	/echo scribe all macro by gimp started

	/declare i int local
	/declare j int local
	
	/if (${Cursor.ID}) /inventory

	/if (!${Window[SpellBookWnd].Open}) /squelch /windowstate SpellBookWnd open

	/for i 1 to 8
		/if (${InvSlot[pack${i}].Item.Container}) {

			/if (!${Window[Pack${i}].Open}) /itemnotify pack${i} rightmouseup
			| need a small delay so bag content can load
			/delay 1

			/for j 1 to ${InvSlot[pack${i}].Item.Container}
				/if (${InvSlot[pack${i}].Item.Item[${j}].Type.Equal["Scroll"]} && ${InvSlot[pack${i}].Item.Item[${j}].Spell.Level} <= ${Me.Level} && !${Me.Book[${InvSlot[pack${i}].Item.Item[${j}].Spell.Name}]}) {

					/echo Please scribe ${InvSlot[pack${i}].Item.Item[${j}].Spell.Name} on cursor
					/itemnotify in pack${i} ${j} leftmouseup
					
					| Need a small delay so pickup event can happen
					/delay 5

					| Loop while user scribes the spell
					:WaitForScribe
					/if (${Cursor.ID}) /goto :WaitForScribe
				}
			/next j

			/if (${Window[Pack${i}].Open}) /itemnotify pack${i} rightmouseup
		}
	/next i

	/if (${Window[SpellBookWnd].Open}) /squelch /windowstate SpellBookWnd close
/return