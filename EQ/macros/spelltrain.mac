|Usage: Memorize low level spells of each type before script is started

#chat tell


#include spell_routines.inc

#define HIDE_FROM_PLAYERS 0

Sub Main

	/declare currentSpell int local 1
	/declare maxSkill int local 0
	/declare skillName string local
	/declare spellName string local

	/target myself
   
	:MainLoop
	/if ( ${Me.Gem[${currentSpell}].ID} ) {

		/echo Casting spell #${currentSpell}, ${Me.Gem[${currentSpell}]}
		/varset skillName ${Me.Gem[${currentSpell}].Skill}
		/varset maxSkill ${Skill[${skillName}].SkillCap}

		/echo ${skillName}: current: ${Me.Skill[${skillName}]}, skill cap: ${maxSkill}

		:SpellCast
		/doevents
		/if (${Me.PctMana} < 20) /call MedBreak

		/if (${Me.Skill[${skillName}]} >= ${maxSkill}) /goto :NextSpell

		:GMCheck
		/if (${SpawnCount[gm]}) {
			/echo A GM or Guide is in zone, pausing for 10 minutes
			/call MedBreak
			/delay 10m
			/goto :GMCheck
		}

		:PlayerCheck
		/if (HIDE_FROM_PLAYERS && ${SpawnCount[pc radius 500 notid ${Me.ID}]}) {
			/echo A player is nearby, pausing
			/call MedBreak
			/delay 2m
			/goto :PlayerCheck
		}

		/call Cast "${Me.Gem[${currentSpell}]}"

		:CursorCheck
		/if (${Cursor.ID}) {
			/autoinv
			/goto :CursorCheck
		}
		/goto :SpellCast
	}
	
	:NextSpell
	/if (${currentSpell} < 9) {
		/varcalc currentSpell ${currentSpell}+1
		/goto :MainLoop
	}

	/call MedBreak

/return


Sub Event_Chat
	/echo [${Time}] Got a tell, stopping
	/endmacro
/return


Sub MedBreak
	/if (${Me.Standing}) /sit
	:MedMore
	/if (${Me.PctMana}<100) /goto :MedMore
/return 